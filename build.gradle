// 与buildscript作用相同
plugins {
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id "org.springframework.boot" version "2.0.5.RELEASE"
}

group 'io.github.toquery'
version '1.0-SNAPSHOT'

allprojects {

    repositories {
//        mavenLocal()
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        mavenCentral()
    }
}

subprojects {
//    apply from: "${project.rootDir}/common_config.gradle"

    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: "io.franzbecker.gradle-lombok"
    apply plugin: 'io.spring.dependency-management'


    group 'io.github.toquery'
    version '1.0-SNAPSHOT'

    sourceCompatibility = 1.8

    dependencies {

        testCompile 'junit:junit:4.12'
        testCompile 'org.springframework.boot:spring-boot-starter-test'
    }


    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
    tasks.withType(Javadoc) {
        options.encoding = "UTF-8"
    }
    if (version =~ 'RELEASE$') {
        signing {
            sign configurations.archives
        }
        uploadArchives {
            repositories {
                mavenDeployer {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: sonatypeUsername, password: sonatypePassword)
                    }

                    pom.project {
                        name project.name
                        packaging 'jar'
                        description 'clever framework by Spring Boot'
                        url 'https://github.com/ToQuery/clever-framework'

                        scm {
                            url 'https://github.com/ToQuery/clever-framework'
                            connection 'https://github.com/ToQuery/clever-framework'
                            developerConnection 'https://github.com/ToQuery/clever-framework'
                        }

                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                distribution 'repo'
                            }
                        }
                        developers {
                            developer {
                                id "ToQuery";
                                name "Shichao Deng";
                                email "toquery@qq.com";
                            }
                        }
                    }
                }
            }
        }
    }
}



